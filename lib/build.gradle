apply plugin: 'com.android.library'
apply plugin: 'maven'

/*
 * Gets the version name from the latest Git tag
 */
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        ignoreExitValue = true
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

version = getVersionName()
group = 'de.jodamob.android'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 12
    }
}

dependencies {
    compile 'org.mockito:mockito-all:1.9.5'
    testCompile 'junit:junit:4.12'
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'mockitoid'
                packaging 'apk'
                description 'Mockito mocks and asserts for android'
                url 'https://github.com/dpreussler/mockitoid'

                scm {
                    url 'scm:git@github.com:dpreussler/mockitoid.git'
                    connection 'scm:git@github.com:dpreussler/mockitoid.git'
                    developerConnection 'scm:git@github.com:dpreussler/mockitoid.git'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'https://github.com/dpreussler/jodamoblog/blob/master/LICENSE'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'dpreussler'
                        name 'Danny Preussler'
                    }
                }
            }
        }
    }
}

